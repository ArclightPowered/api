plugins {
    id 'java'
    id 'maven-publish'
}

group 'io.izzel.arclight'
version '1.2.5'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://files.minecraftforge.net/maven/' }
}

dependencies {
    compileOnly 'org.ow2.asm:asm:9.2'
    compileOnly 'org.ow2.asm:asm-tree:9.2'
    compileOnly "org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT"
    compileOnly "net.minecraftforge:eventbus:4.0.0"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

java {
    withSourcesJar()
    withJavadocJar()
}

compileJava {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = "IzzelAliz"
            url = uri("https://maven.izzel.io/releases")
            credentials {
                username = project.findProperty("mavenUser") ?: System.getenv("USERNAME")
                password = project.findProperty("mavenToken") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
